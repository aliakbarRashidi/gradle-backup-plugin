/*
 * Copyright 2015 madhead <siarhei.krukau@gmail.com>
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
	id 'groovy'
	id 'maven-publish'
	id 'com.jfrog.bintray' version '1.5'
	id 'com.jfrog.artifactory' version '3.2.0'
	id 'net.researchgate.release' version '2.3.4'
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
	mavenCentral()
}

dependencies {
	compile localGroovy()
	compile gradleApi()
	compile 'com.google.apis:google-api-services-drive:v3-rev1-1.19.1'
	compile 'org.fusesource.jansi:jansi:1.11'
	compile 'commons-io:commons-io:2.4'
}

tasks.withType(Javadoc) {
	options.links(
			'http://docs.oracle.com/javase/7/docs/api/',
			'http://docs.groovy-lang.org/latest/html/api/',
			'https://gradle.org/docs/current/javadoc/'
	)
}

jar {
	from 'LICENSE'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc', 'LICENSE'
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource, 'LICENSE'
}

publishing {
	publications {
		main(MavenPublication) {
			from components.java

			artifact sourcesJar
			artifact javadocJar
		}
	}
}

bintray {
	user = System.getenv('GRADLE_BACKUP_PLUGIN_BINTRAY_USER')
	key = System.getenv('GRADLE_BACKUP_PLUGIN_BINTRAY_KEY')
	publications = ['main']
	publish = true

	pkg {
		repo = 'gradle-plugins'
		name = 'gradle-backup-plugin'
		websiteUrl = 'https://github.com/madhead/gradle-backup-plugin'
		issueTrackerUrl = 'https://github.com/madhead/gradle-backup-plugin/issues'
		vcsUrl = 'https://github.com/madhead/gradle-backup-plugin.git'

		licenses = ['Apache-2.0']
	}
}

artifactory {
	contextUrl = 'https://oss.jfrog.org/artifactory'

	publish {
		repository {
			repoKey = 'oss-snapshot-local'
			username = System.getenv('GRADLE_BACKUP_PLUGIN_BINTRAY_USER')
			password = System.getenv('GRADLE_BACKUP_PLUGIN_BINTRAY_KEY')
		}
	}
}

artifactoryPublish {
	publications(publishing.publications.main)

	onlyIf {
		System.getenv('GRADLE_BACKUP_PLUGIN_ARTIFACTORY_PUBLISH')?.toBoolean()
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.9'
}
